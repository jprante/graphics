
def junitVersion = project.hasProperty('junit.version')?project.property('junit.version'):'5.7.0'
def hamcrestVersion = project.hasProperty('hamcrest.version')?project.property('hamcrest.version'):'2.2'

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
    failFast = false
    environment 'TMPDIR', '/var/tmp/gs'
    file('/var/tmp/gs').mkdirs()
    systemProperty 'java.awt.headless', 'true'
    systemProperty 'java.io.tmpdir', '/var/tmp/'
    systemProperty 'jna.tmpdir', '/var/tmp/'
    systemProperty 'jna.debug', 'false'
    systemProperty 'java.util.logging.config.file', 'src/test/resources/logging.properties'
    testLogging {
        events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams = true
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
